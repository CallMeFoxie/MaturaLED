/*
 * boot.asm
 *
 *  Created: 18.2.2012 19:13:16
 *   Author: Ondra
 */ 

START_MEMTEST:
	; start at RAMEND+1 till the end. 
	; X: pointer to memory, R16: current number. First, write it all there, second run: read it back
	LDI		XL, LOW(RAMEND+1)
	LDI		XH, HIGH(RAMEND+1)
	LDI		R16, 0
	LDI		R17, 0xFF
BOOT_MEMTEST_WRITE:
	CPI16	XL, XH, XRAMEND
	BREQ	BOOT_MEMTEST_READ_B0
	SETBANK0
	ST		X, R16
	SETBANK1
	ST		X+, R17
	INC		R16
	DEC		R17
	RJMP	BOOT_MEMTEST_WRITE
BOOT_MEMTEST_READ_B0:
	PC_Send_String STR_BANK0
	SETBANK0
	LDI		XL, LOW(RAMEND+1)
	LDI		XH, HIGH(RAMEND+1)
	LDI		R16, 0xFF ; one less than 0
BOOT_MEMTEST_READ_B0_LOOP:
	CPI16	XL, XH, XRAMEND
	BREQ	BOOT_MEMTEST_READ_B1
	INC		R16
	LD		R17, X+
	CP		R16, R17
	BREQ	BOOT_MEMTEST_READ_B0_LOOP
	; here it ends up only if it didn't work
	PC_Send_String	STR_MEMTEST_FAIL
	RJMP	BOOT_MEMTEST_FAIL
BOOT_MEMTEST_READ_B1:
	PC_Send_String STR_MEMTEST_OK
	PC_Send_String STR_BANK1
	SETBANK1
	LDI		XL, LOW(RAMEND+1)
	LDI		XH, HIGH(RAMEND+1)
	LDI		R16, 0 ; one more than FF
BOOT_MEMTEST_READ_B1_LOOP:
	CPI16	XL, XH, XRAMEND
	BREQ	BOOT_MEMTEST_OK
	DEC		R16
	LD		R17, X+
	CP		R16, R17
	BREQ	BOOT_MEMTEST_READ_B1_LOOP
	PC_Send_String STR_MEMTEST_FAIL
BOOT_MEMTEST_FAIL:
	NOP
	RJMP BOOT_MEMTEST_FAIL
BOOT_MEMTEST_OK:
	PC_Send_String STR_MEMTEST_OK
	; now clean it all up
	LDI		XL, LOW(RAMEND+1)
	LDI		XH, HIGH(RAMEND+1)
	LDI		R16, 0x00
BOOT_MEMTEST_CLEAN:
	CPI16	XL, XH, XRAMEND
	BREQ	BOOT_MEMTEST_RETURN
	SETBANK0
	ST		X, R16
	SETBANK1
	ST		X+, R16
	;ADIW	X, 1
	RJMP	BOOT_MEMTEST_CLEAN
BOOT_MEMTEST_RETURN:
	SETBANK0
	PC_Send_String STR_START
	RET

START_JTRF:
	PC_Send_String STR_JTRF
	RJMP	BOOT_FINISH
	;RJMP	BOOT_MEMTEST

START_WDRF:
	PC_Send_String STR_WDRF
	RJMP	BOOT_FINISH

START_BORF:
	PC_Send_String STR_BORF
	RJMP	BOOT_MEMTEST

START_EXTRF:
	PC_Send_String STR_EXTRF
	RJMP	BOOT_FINISH

START_PORF:
	PC_Send_String STR_PORF
	RJMP	BOOT_MEMTEST